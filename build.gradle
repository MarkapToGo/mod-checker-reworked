plugins {
    id 'architectury-plugin' version '3.4-SNAPSHOT'
    id 'dev.architectury.loom' version '1.13.463' apply false
    id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
    id 'java'
}

architectury {
    minecraft = project.minecraft_version
}

allprojects {
    group = project.maven_group
    version = project.mod_version
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'architectury-plugin'
    apply plugin: 'dev.architectury.loom'

    base {
        archivesName = "${project.archives_base_name}-${project.name}"
    }

    repositories {
        maven { url "https://maven.architectury.dev/" }
        maven { url "https://maven.fabricmc.net/" }
        maven { url "https://maven.neoforged.net/releases/" }
    }

    dependencies {
        minecraft "com.mojang:minecraft:${project.minecraft_version}"
        mappings loom.officialMojangMappings()
    }

    java {
        withSourcesJar()
        toolchain.languageVersion = JavaLanguageVersion.of(21)
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
        options.release = 21
    }
}

// Disable the common module transform tasks since we are manualy handling platform abstraction
project(':common') {
    tasks.configureEach { task ->
        if (task.name.startsWith('transformProduction')) {
            task.enabled = false
        }
    }
}
